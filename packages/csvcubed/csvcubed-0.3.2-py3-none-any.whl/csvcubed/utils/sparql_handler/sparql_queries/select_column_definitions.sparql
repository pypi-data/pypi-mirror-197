PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX csvw: <http://www.w3.org/ns/csvw#>

SELECT
        ?csvUrl
        ?aboutUrl
        ?dataType
        ?name
        ?propertyUrl
        (COALESCE(?requiredO, false) as ?required)
        (COALESCE(?suppressOutputO, false) as ?suppressOutput)
        ?title
        ?valueUrl
        (COALESCE(?virtualO, false) as ?virtual)
WHERE {

    {
        # Code List CSV-W tableSchemas are referenced from two locations:
        #  * the cube they're part of
        #  * the `.csv-metadata.json` file which defines the Code List independently.
        # We load both of these into the rdf_graph so we end up with two triples saying:
        #   []  csvw:tableSchema ?tableSchema.
        # This introduces duplicates to the query which we can remove with this DISTINCT sub-query.
        SELECT DISTINCT ?csvUrl ?tableSchema
        WHERE {
            [] csvw:tableSchema ?tableSchema;
               csvw:url ?csvUrl.
        }
    }

    ?tableSchema csvw:column/rdf:rest*/rdf:first ?column

    OPTIONAL { ?column csvw:aboutUrl ?aboutUrl. }
    OPTIONAL { ?column csvw:datatype ?dataType. }
    OPTIONAL { ?column csvw:name ?name. }
    OPTIONAL { ?column csvw:propertyUrl ?propertyUrl. }
    OPTIONAL { ?column csvw:required ?requiredO. }
    OPTIONAL { ?column csvw:suppressOutput ?suppressOutputO. }
    OPTIONAL { ?column csvw:title ?title. }
    OPTIONAL { ?column csvw:valueUrl ?valueUrl. }
    OPTIONAL { ?column csvw:virtual ?virtualO. }
}
